/*
 * Copyright (C) 2014 SCVNGR, Inc. d/b/a LevelUp
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except
 * in compliance with the License. You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the License
 * is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express
 * or implied. See the License for the specific language governing permissions and limitations under
 * the License.
 */

/*
 * NOTE: To avoid a circular dependency, this library's tests live in levelupCoreTestLib instead of
 * inside this project where they would normally go.
 */
apply plugin: 'com.android.library'

android {
    compileSdkVersion 19
    buildToolsVersion '20.0.0'

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 19
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        release {
            description 'Library-release build with packaged Proguard configuration'
            runProguard false
            consumerProguardFiles 'proguard-project.txt'
        }
    }
}

android.libraryVariants.all { variant ->

    task("generate${variant.name.capitalize()}Javadoc",
         type: Javadoc,
         dependsOn: ["process${variant.name.capitalize()}Resources",
                     ":levelUpDeeplinkAuthLib:assemble${variant.name.capitalize()}"]) {
        description "Generates Javadoc for $variant.name."
        source = files(variant.javaCompile.source) +
                 files(variant.processJavaResources.source) +
                 files("${project(':levelUpDeeplinkAuthLib').buildDir}/generated/source/buildConfig/${variant.name}/") +
                 files("${project(':levelUpDeeplinkAuthLib').buildDir}/generated/source/r/${variant.name}/") +
                 files("${project(':levelUpDeeplinkAuthLib').projectDir}/src/main/java/")
        ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
        classpath = files(variant.javaCompile.classpath.files) + files(ext.androidJar)
        options {
            linksOffline 'http://d.android.com/reference', "${android.sdkDirectory}/docs/reference"
            charSet = 'UTF-8'
            use = true
            splitIndex = true
        }
    }
}

dependencies {
    compile 'com.android.support:support-v4:20.0.+'
    compile 'com.google.code.gson:gson:2.2.4'
    compile 'com.github.stephenc.jcip:jcip-annotations:1.0-1'
    compile files('libs/braintree-android-encryption-1.0.0.jar')
    provided 'org.projectlombok:lombok:1.12.6'
}
